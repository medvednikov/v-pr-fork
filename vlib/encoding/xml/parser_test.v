module xml

const sample_doc = '
<root>
	<c id="c1"/>
	<c id="c2">
		Sample Text
	</c>
	<empty/>
	<c id="c3"/>
	<abc id="c4"/>
	<xyz id="c5"/>
	<c id="c6"/>
	<cx id="c7"/>
	<cd id="c8"/>
	<child id="c9">
		More Sample Text
	</child>
	<cz id="c10"/>
</root>'

const xml_elements = [
	XMLNode{
		name:       'c'
		attributes: {
			'id': 'c1'
		}
	},
	XMLNode{
		name:       'c'
		attributes: {
			'id': 'c2'
		}
		children:   [
			'Sample Text',
		]
	},
	XMLNode{
		name:       'empty'
		attributes: {}
	},
	XMLNode{
		name:       'c'
		attributes: {
			'id': 'c3'
		}
	},
	XMLNode{
		name:       'abc'
		attributes: {
			'id': 'c4'
		}
	},
	XMLNode{
		name:       'xyz'
		attributes: {
			'id': 'c5'
		}
	},
	XMLNode{
		name:       'c'
		attributes: {
			'id': 'c6'
		}
	},
	XMLNode{
		name:       'cx'
		attributes: {
			'id': 'c7'
		}
	},
	XMLNode{
		name:       'cd'
		attributes: {
			'id': 'c8'
		}
	},
	XMLNode{
		name:       'child'
		attributes: {
			'id': 'c9'
		}
		children:   [
			'More Sample Text',
		]
	},
	XMLNode{
		name:       'cz'
		attributes: {
			'id': 'c10'
		}
	},
]

fn test_single_element_parsing() ! {
	mut reader := FullBufferReader{
		contents: sample_doc.bytes()
	}
	// Skip the "<root>" tag
	mut skip := []u8{len: 6}
	reader.read(mut skip)!

	mut local_buf := [u8(0)]
	mut ch := next_char(mut reader, mut local_buf)!

	mut count := 0

	for count < xml_elements.len {
		match ch {
			`<` {
				next_ch := next_char(mut reader, mut local_buf)!
				match next_ch {
					`/` {}
					else {
						parsed_element := parse_single_node(next_ch, mut reader)!
						assert xml_elements[count] == parsed_element
						count++
					}
				}

				ch = next_char(mut reader, mut local_buf)!
			}
			else {
				for ch != `<` {
					ch = next_char(mut reader, mut local_buf)!
				}
			}
		}
	}
}

fn test_parser_decodes_entities_in_text_and_attributes() {
	doc := XMLDocument.from_string('<r a="&lt;&gt;&amp;&quot;&apos;">&lt;&gt;&amp;&quot;&apos;</r>')!
	assert doc.root.attributes['a'] == '<>&"\''
	assert doc.root.children == [XMLNodeContents('<>&"\'')]
	assert XMLDocument.from_string(doc.str())!.root == doc.root
}

fn test_parser_decodes_numeric_character_references() {
	doc := XMLDocument.from_string('<r a="&#65;&#x42;&#x1F600;">&#65;&#x42;&#128512;&#32;</r>')!
	assert doc.root.attributes['a'] == 'AB😀'
	assert doc.root.children == [XMLNodeContents('AB😀 ')]
	space := XMLDocument.from_string('<r>&#32;<c/>&#x20;</r>')!
	assert space.root.children == [XMLNodeContents(' '), XMLNode{ name: 'c' }, ' ']
}

fn test_parser_rejects_invalid_entity_references() {
	for reference in ['&unknown;', '&amp', '&#0;', '&#xD800;', '&#xFFFE;', '&#x110000;',
		'&#999999999999999999999;', '&#;', '&#x;', '&#-1;', '&#xG;', '&#1;'] {
		for input in ['<r>${reference}</r>', '<r a="${reference}"/>'] {
			if doc := XMLDocument.from_string(input) {
				assert false, 'accepted ${input}: ${doc}'
			} else {
				assert err.msg().len > 0
			}
		}
	}
}

fn test_parser_preserves_cdata_and_decodes_separate_text_runs() {
	doc := XMLDocument.from_string('<r>a&amp;<!--c-->b&lt;<![CDATA[&amp;]]>c&gt;</r>')!
	assert doc.root.children == [XMLNodeContents('a&'), XMLComment{ text: 'c' }, 'b<',
		XMLCData{ text: '&amp;' }, 'c>']
}

fn test_validation_does_not_decode_entities_twice() {
	doc := XMLDocument.from_string('<?xml version="1.0"?><!DOCTYPE r [<!ELEMENT r (#PCDATA)>]><r>&amp;amp;</r>')!
	assert doc.root.children == [XMLNodeContents('&amp;')]
	assert doc.validate()!.root == doc.root
}

fn test_parser_decodes_declared_entities() {
	doc := XMLDocument.from_string('<?xml version="1.0"?><!DOCTYPE r [<!ENTITY warning "hello">]><r a="&warning;">&warning;</r>')!
	assert doc.root.attributes['a'] == 'hello'
	assert doc.root.children == [XMLNodeContents('hello')]
	assert doc.validate()!.root == doc.root
}
